/// build identical bitmaps in zroaring and croaring from values.
/// serialize both, compare bytes. cross deserialize, verify contents.
fn validateRoundTrip(allocator: mem.Allocator, io: Io, name: @EnumLiteral(), values: []const u32, run_optimize: bool) !void {
    misc.trace(@src(), "{s}", .{@tagName(name)});
    var zr: Bitmap = .empty;
    defer zr.deinit(allocator);
    _ = try zr.add_many(allocator, values);
    var reason: ?[]const u8 = null;
    if (!zr.internal_validate(&reason)) {
        misc.trace(@src(), "validation failed: {s}", .{reason.?});
        return error.Invalid;
    }

    for (values, 0..) |v, i| {
        testing.expect(zr.contains(v)) catch |e| {
            const hb, const lb = [2]u16{ @truncate(v >> 16), @truncate(v) };
            const a = zr.get_header();
            std.debug.print("Bitmap missing value i {}, v {}:{x} hb/lb {}/{}:{x}/{x}, containers {}\n", .{ i, v, v, hb, lb, hb, lb, a.len });
            std.debug.print("  keys {any}\n", .{a.slice(.keys, .len)});
            std.debug.print("  values {any} index {}\n", .{ values, zr.get_index(v) });
            const c1 = a.containers[@intCast(zr.get_index(v))];
            const slice = misc.asSlice([]const u16, zr.blocks.items[c1.blockoffset..][0..c1.nblocks()])[0..c1.cardinality];
            std.debug.print("  array {any}\n", .{slice});
            return e;
        };
    }
    if (run_optimize) _ = try zr.run_optimize(allocator);

    // std.debug.print("zr {any}\n", .{zr});
    // _ = name;
    // std.debug.print("{s} zr cards {any}\n", .{ name, zr.get_cards() });
    try testing.expectEqual(values.len, zr.cardinality());

    // build coaring bitmap
    const cr = c.roaring_bitmap_create().?;
    defer c.roaring_bitmap_free(cr);
    for (values) |v| c.roaring_bitmap_add(cr, v);
    if (run_optimize) _ = c.roaring_bitmap_run_optimize(cr);

    // check size in bytes equal
    const cr_header_size = c.ra_portable_header_size(&cr.*.high_low_container);
    const zr_header_size = zr.portable_header_size();
    try testing.expectEqual(cr_header_size, zr_header_size);
    const zr_size = zr.portable_size_in_bytes();
    const cr_size = c.roaring_bitmap_portable_size_in_bytes(cr);
    try testing.expectEqual(cr_size, zr_size);

    // serialize both
    const zr_serbuf = try allocator.alloc(u8, zr_size);
    defer allocator.free(zr_serbuf);
    var zr_w = std.Io.Writer.fixed(zr_serbuf);
    const cr_serbuf = try allocator.alloc(u8, cr_size);
    defer allocator.free(cr_serbuf);
    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();
    try testing.expectEqual(
        c.roaring_bitmap_portable_serialize(cr, @ptrCast(cr_serbuf.ptr)),
        zr.portable_serialize(&zr_w, arena.allocator()),
    );
    // std.debug.print("'{s}' values {any}\nzr {f}\n", .{ name, values[0..@min(20, values.len)], zr });
    // std.debug.print("cr_buf {x}\nzr_buf {x}\n", .{ cr_buf, zr_buf });
    try testing.expectEqualSlices(u8, cr_serbuf, zr_serbuf);

    // deserialize zr bytes with croaring. check equal.
    const cr2 = c.roaring_bitmap_portable_deserialize_safe(@ptrCast(zr_serbuf.ptr), zr_serbuf.len);
    try testing.expect(cr2 != null);
    defer c.roaring_bitmap_free(cr2);
    try testing.expectEqual(c.roaring_bitmap_get_cardinality(cr2), zr.cardinality());
    for (values) |v| try testing.expect(c.roaring_bitmap_contains(cr2, v));

    // deserialize croaring bytes with zroaring. check equal.
    var tmpdir = testing.tmpDir(.{});
    defer tmpdir.cleanup();
    { // write cr_buf to file
        const cr_f = try tmpdir.dir.createFile(io, "cr_f", .{});
        defer cr_f.close(io);
        try cr_f.writeStreamingAll(io, cr_serbuf);
    }
    var rbuf: [256]u8 = undefined;
    const cr_f = try tmpdir.dir.openFile(io, "cr_f", .{});
    var zr2 = try Bitmap.portable_deserialize(allocator, io, cr_f, &rbuf);
    defer zr2.deinit(allocator);
    // std.debug.print("zr2 {f}\n", .{zr2});
    // std.debug.print("zr2 {} zr {any}\n", .{ zr, zr2, cr_buf.len, zr });
    // std.debug.print("zr2 card {} zr card {}\n", .{ zr2.get_cardinality(), zr.get_cardinality() });
    try testing.expectEqual(zr2.cardinality(), zr.cardinality());
    try testing.expect(zr2.equals(&zr));
}

/// Validate using addRange instead of individual adds.
fn validateRangeRoundTrip(allocator: mem.Allocator, io: Io, name: []const u8, start: u32, end: u32, run_optimize: bool) !void {
    _ = name;
    // build both
    var zr: Bitmap = .empty;
    defer zr.deinit(allocator);
    try zr.add_range(allocator, start, end + 1);
    misc.trace(@src(), "after add_range({},{}) zr {}", .{ start, end + 1, zr.get_header() });

    const zr_did_optimize = run_optimize and try zr.run_optimize(allocator);

    const cr = c.roaring_bitmap_create() orelse return error.CRoaringAllocFailed;
    defer c.roaring_bitmap_free(cr);
    c.roaring_bitmap_add_range(cr, start, @as(u64, end) + 1);
    const cr_did_optimize = run_optimize and c.roaring_bitmap_run_optimize(cr);

    // serialize both
    const cr_size = c.roaring_bitmap_portable_size_in_bytes(cr);
    const cr_buf = try allocator.alloc(u8, cr_size);
    defer allocator.free(cr_buf);

    const zr_size = zr.portable_size_in_bytes();
    // std.debug.print("zr_size {}\n", .{zr_size});
    const zr_buf = try allocator.alloc(u8, zr_size);
    defer allocator.free(zr_buf);
    var zr_w: std.Io.Writer = .fixed(zr_buf);
    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();
    _ = try zr.portable_serialize(&zr_w, arena.allocator());

    _ = c.roaring_bitmap_portable_serialize(cr, @ptrCast(cr_buf.ptr));
    try testing.expectEqual(cr_did_optimize, zr_did_optimize);
    try testing.expectEqual(cr_size, zr_size);
    try testing.expectEqualSlices(u8, cr_buf, zr_buf);

    // deserialize zr bytes with croaring
    const cr2 = c.roaring_bitmap_portable_deserialize_safe(@ptrCast(zr_buf.ptr), zr_size);
    try testing.expect(cr2 != null);
    defer c.roaring_bitmap_free(cr2);
    try testing.expectEqual(c.roaring_bitmap_get_cardinality(cr2), zr.cardinality());

    // deserialize croaring bytes with zr
    var tmpdir = testing.tmpDir(.{});
    defer tmpdir.cleanup();
    { // write cr_buf to file
        const cr_f = try tmpdir.dir.createFile(io, "cr_f", .{});
        defer cr_f.close(io);
        try cr_f.writeStreamingAll(io, cr_buf);
    }
    var rbuf: [256]u8 = undefined;
    const cr_f = try tmpdir.dir.openFile(io, "cr_f", .{});
    var zr2 = try Bitmap.portable_deserialize(allocator, io, cr_f, &rbuf);
    defer zr2.deinit(allocator);
    try testing.expect(zr.equals(&zr2));

    if (true) return;
}

/// Validate FrozenBitmap can read serialized bytes and contains() works correctly.
fn validateFrozenContains(allocator: mem.Allocator, name: []const u8, values: []const u32, run_optimize: bool) !void {
    _ = name; // autofix

    // Build both and serialize frozen
    const cr = c.roaring_bitmap_create();
    defer c.roaring_bitmap_free(cr);
    c.roaring_bitmap_add_many(cr, values.len, values.ptr);
    if (run_optimize) _ = c.roaring_bitmap_run_optimize(cr);
    const cr_frozen_buf = try allocator.alloc(u8, c.roaring_bitmap_frozen_size_in_bytes(cr));
    defer allocator.free(cr_frozen_buf);
    c.roaring_bitmap_frozen_serialize(cr, cr_frozen_buf.ptr);
    // std.debug.print("{s} cr_frozen_size {}\n", .{ name, cr_frozen_size });

    var zr: Bitmap = .{};
    defer zr.deinit(allocator);
    try zr.add_many(allocator, values);
    if (true) unreachable;
    if (run_optimize) _ = try zr.run_optimize(allocator);
    const zr_frozen_buf = try allocator.alignedAlloc(u8, .@"32", zr.frozen_size_in_bytes());
    defer allocator.free(zr_frozen_buf);
    try zr.frozen_serialize(zr_frozen_buf);
    try testing.expectEqualSlices(u8, cr_frozen_buf, zr_frozen_buf);

    var zr_frozen = try Bitmap.frozen_view(allocator, zr_frozen_buf);
    defer zr_frozen.deinit(allocator);
    for (values) |v| try testing.expect(zr_frozen.contains(v));
    try testing.expect(zr.equals(zr_frozen));
}

const testio = testing.io;

fn validateAll(allocator: mem.Allocator) !void {
    // Basic tests:
    try validateRoundTrip(allocator, testio, .empty, &.{}, false);
    try validateRoundTrip(allocator, testio, .single_zero, &.{0}, false);
    try validateRoundTrip(allocator, testio, .single_max, &.{0xFFFFFFFF}, false);
    try validateRoundTrip(allocator, testio, .single_mid, &.{1000000}, false);

    // Array container tests:
    var arr100: [100]u32 = undefined; // Small array
    for (0..100) |i| arr100[i] = @intCast(i * 10);
    try validateRoundTrip(allocator, testio, .array_100, &arr100, false);
    var arr4096: [4096]u32 = undefined; // Array at threshold (4096 = max array size)
    for (0..4096) |i| arr4096[i] = @intCast(i);
    try validateRoundTrip(allocator, testio, .array_4096, &arr4096, false);

    // Bitset container tests:
    var bitset5000: [5000]u32 = undefined; // Just over threshold -> bitset
    for (0..5000) |i| bitset5000[i] = @intCast(i);
    try validateRoundTrip(allocator, testio, .bitset_5000, &bitset5000, false);

    if (true) return;
    // Full chunk as run (65536 values) - CRoaring auto-optimizes to run, so we must too
    // (This tests run serialization, not bitset - renamed to avoid confusion)
    try validateRangeRoundTrip(allocator, testio, "run_full_chunk", 0, 65535, true);

    // Multiple container tests:
    // Values at chunk boundaries
    try validateRoundTrip(allocator, testio, .chunk_boundaries, &.{ 65535, 65536, 131071, 131072 }, false);
    // 3 containers (below NO_OFFSET_THRESHOLD for run format)
    var three_containers: [3]u32 = .{ 100, 65536 + 100, 131072 + 100 };
    try validateRoundTrip(allocator, testio, .three_containers, &three_containers, false);
    // 4 containers (at NO_OFFSET_THRESHOLD)
    var four_containers: [4]u32 = .{ 100, 65536 + 100, 131072 + 100, 196608 + 100 };
    try validateRoundTrip(allocator, testio, .four_containers, &four_containers, false);
    // 5+ containers
    var five_containers: [5]u32 = .{ 100, 65536 + 100, 131072 + 100, 196608 + 100, 262144 + 100 };
    try validateRoundTrip(allocator, testio, .five_containers, &five_containers, false);

    // Run-optimized tests:
    // Range that compresses well
    try validateRangeRoundTrip(allocator, "range_0_1000", 0, 1000, true);
    try validateRangeRoundTrip(allocator, "range_0_10000", 0, 10000, true);
    // Multiple ranges -> multiple runs
    var multi_range: [300]u32 = undefined;
    for (0..100) |i| {
        multi_range[i] = @intCast(i); // 0-99
        multi_range[100 + i] = @intCast(500 + i); // 500-599
        multi_range[200 + i] = @intCast(1000 + i); // 1000-1099
    }
    try validateRoundTrip(allocator, testio, .multi_range_runs, &multi_range, true);
    // Alternating values (doesn't compress to runs)
    var alternating: [100]u32 = undefined;
    for (0..100) |i| alternating[i] = @intCast(i * 2); // 0, 2, 4, 6...
    try validateRoundTrip(allocator, testio, .alternating_no_runs, &alternating, true);
    // 4+ containers with run_optimize - exercises run format WITH offset header
    // (NO_OFFSET_THRESHOLD = 4, so this triggers offset header in run format)
    var four_chunks_runs: [400]u32 = undefined;
    for (0..100) |i| four_chunks_runs[i] = @intCast(i); // chunk 0: 0-99
    for (0..100) |i| four_chunks_runs[100 + i] = @intCast(65536 + i); // chunk 1
    for (0..100) |i| four_chunks_runs[200 + i] = @intCast(131072 + i); // chunk 2
    for (0..100) |i| four_chunks_runs[300 + i] = @intCast(196608 + i); // chunk 3
    try validateRoundTrip(allocator, testio, .four_chunks_run_optimized, &four_chunks_runs, true);

    // Large scale tests:
    // Dense range (1M values) - CRoaring auto-optimizes ranges, so we must too
    try validateRangeRoundTrip(allocator, "dense_1M", 0, 999999, true);

    // Sparse random (N values across u32 space)
    const N = if (std.debug.runtime_safety) 2000 else 500000;
    var prng = std.Random.DefaultPrng.init(0);
    const sparse_N = try allocator.alloc(u32, N);
    defer allocator.free(sparse_N);
    for (sparse_N) |*x| x.* = prng.random().int(u32);
    // Sort and dedupe for consistent results
    std.mem.sort(u32, sparse_N, {}, std.sort.asc(u32));
    var deduped_len: usize = 1;
    for (1..N) |i| {
        if (sparse_N[i] != sparse_N[deduped_len - 1]) {
            sparse_N[deduped_len] = sparse_N[i];
            deduped_len += 1;
        }
    }
    try validateRoundTrip(allocator, testio, .sparse_N, sparse_N[0..deduped_len], false);

    // validate frozen_view can read serialized bytes correctly
    try validateFrozenContains(allocator, "frozen_array", &arr100, false);
    try validateFrozenContains(allocator, "frozen_bitset", &bitset5000, false);
    try validateFrozenContains(allocator, "frozen_run_single_chunk", &multi_range, true);
    try validateFrozenContains(allocator, "frozen_run_with_offsets", &four_chunks_runs, true);
    try validateFrozenContains(allocator, "frozen_multi_container", &five_containers, false);
}

const testgpa = testing.allocator;

test validateAll {
    try validateAll(testgpa);
}

test "allocation failures" {
    try testing.checkAllAllocationFailures(testgpa, validateAll, .{});
}

fn validateTestdata(io: Io, filepath: []const u8) !void {
    const f = try Io.Dir.cwd().openFile(io, filepath, .{});
    defer f.close(io);
    var rbuf: [256]u8 = undefined;
    var rb = try Bitmap.portable_deserialize(testgpa, io, f, &rbuf);
    defer rb.deinit(testgpa);

    // > That is, they contain all multiplies of 1000 in [0,100000), all multiplies of 3 in [100000,200000) and all values in [700000,800000).
    // > https://github.com/RoaringBitmap/RoaringFormatSpec/tree/master/testdata
    var k: u32 = 0;
    while (k < 100000) : (k += 1000) {
        testing.expect(rb.contains(k)) catch |e| {
            std.debug.print("missing {}\n", .{k});
            std.debug.print("{f}\n", .{rb.get_header()});
            return e;
        };
    }

    k = 100000;
    while (k < 200000) : (k += 1)
        try testing.expect(rb.contains(3 * k));

    k = 700000;
    while (k < 800000) : (k += 1)
        try testing.expect(rb.contains(k));
}

test "without runs" {
    try validateTestdata(testing.io, "testdata/bitmapwithoutruns.bin");
}

test "with runs" {
    try validateTestdata(testing.io, "testdata/bitmapwithruns.bin");
}

const std = @import("std");
const mem = std.mem;
const Io = std.Io;
const testing = std.testing;
const zroaring = @import("root.zig");
const Bitmap = zroaring.Bitmap;
const c = @import("c.zig").root;
const misc = @import("misc.zig");
