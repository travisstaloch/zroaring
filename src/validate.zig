/// build identical bitmaps in zroaring and croaring from values.
/// serialize both, compare bytes. cross deserialize, verify contents.
fn validateRoundTrip(allocator: mem.Allocator, name: []const u8, values: []const u32, run_optimize: bool) !void {
    _ = name;
    // build zroaring bitmap
    var zr: Bitmap = .{};
    defer zr.deinit(allocator);
    try zr.add_many(allocator, values);
    for (values, 0..) |v, i| {
        testing.expect(zr.contains(v)) catch |e| {
            const hb, const lb = [2]u16{ @truncate(v >> 16), @truncate(v) };
            std.debug.print("Bitmap missing value i {}, v {}:{x} hb/lb {}/{}:{x}/{x}, containers {}\n", .{ i, v, v, hb, lb, hb, lb, zr.high_low_container.kvs.len });
            std.debug.print("hlc keys {any}\n", .{zr.high_low_container.kvs.items(.key)});
            std.debug.print("values {any} index {}\n", .{ values, zr.get_index(v) });
            const vc = zr.high_low_container.kvs.items(.container)[@intCast(zr.get_index(v))];
            std.debug.print("  {any}\n", .{vc.const_cast(.array).slice()});
            // std.debug.print("{f}\n", .{zr});
            return e;
        };
    }
    if (run_optimize) _ = try zr.run_optimize(allocator);
    // std.debug.print("zr {any}\n", .{zr});
    // std.debug.print("{s} zr {f}\n", .{ name, zr });
    try testing.expectEqual(values.len, zr.cardinality());

    // build coaring bitmap
    const cr = c.roaring_bitmap_create().?;
    defer c.roaring_bitmap_free(cr);
    for (values) |v| c.roaring_bitmap_add(cr, v);
    if (run_optimize) _ = c.roaring_bitmap_run_optimize(cr);

    // check size in bytes equal
    const zr_header_size = zr.high_low_container.portable_header_size();
    const cr_header_size = c.ra_portable_header_size(&cr.*.high_low_container);
    try testing.expectEqual(cr_header_size, zr_header_size);
    const zr_size = zr.portable_size_in_bytes();
    const cr_size = c.roaring_bitmap_portable_size_in_bytes(cr);
    try testing.expectEqual(cr_size, zr_size);

    // serialize both
    const zr_buf = try allocator.alloc(u8, zr_size);
    defer allocator.free(zr_buf);
    var zr_w = std.Io.Writer.fixed(zr_buf);
    const cr_buf = try allocator.alloc(u8, cr_size);
    defer allocator.free(cr_buf);
    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();
    try testing.expectEqual(
        c.roaring_bitmap_portable_serialize(cr, @ptrCast(cr_buf.ptr)),
        zr.portable_serialize(&zr_w, arena.allocator()),
    );
    // std.debug.print("'{s}' values {any}\nzr {f}\n", .{ name, values[0..@min(20, values.len)], zr });
    // std.debug.print("cr_buf {x}\nzr_buf {x}\n", .{ cr_buf, zr_buf });
    try testing.expectEqualSlices(u8, cr_buf, zr_buf);

    // deserialize zr bytes with croaring. check equal.
    const cr2 = c.roaring_bitmap_portable_deserialize_safe(@ptrCast(zr_buf.ptr), zr_buf.len);
    try testing.expect(cr2 != null);
    defer c.roaring_bitmap_free(cr2);
    try testing.expectEqual(c.roaring_bitmap_get_cardinality(cr2), zr.cardinality());
    for (values) |v| try testing.expect(c.roaring_bitmap_contains(cr2, v));

    // deserialize croaring bytes with zroaring. check equal.
    var cr_r = std.Io.Reader.fixed(cr_buf);
    var zr2 = try Bitmap.portable_deserialize(allocator, &cr_r);
    defer zr2.deinit(allocator);
    // std.debug.print("zr2 {f}\n", .{zr2});
    // std.debug.print("zr2 {} zr {any}\n", .{ zr, zr2, cr_buf.len, zr });
    // std.debug.print("zr2 card {} zr card {}\n", .{ zr2.get_cardinality(), zr.get_cardinality() });
    try testing.expectEqual(zr2.cardinality(), zr.cardinality());
    try testing.expect(zr2.equals(zr));
}

/// Validate using addRange instead of individual adds.
fn validateRangeRoundTrip(allocator: mem.Allocator, name: []const u8, start: u32, end: u32, run_optimize: bool) !void {
    _ = name;
    // build both
    var zr: Bitmap = .{};
    defer zr.deinit(allocator);
    try zr.add_range(allocator, start, end + 1);
    // std.debug.print("after add_range({},{}) zr {f}\n", .{ start, end + 1, zr });
    const zr_did_optimize = run_optimize and try zr.run_optimize(allocator);

    const cr = c.roaring_bitmap_create() orelse return error.CRoaringAllocFailed;
    defer c.roaring_bitmap_free(cr);
    c.roaring_bitmap_add_range(cr, start, @as(u64, end) + 1);
    const cr_did_optimize = run_optimize and c.roaring_bitmap_run_optimize(cr);

    // serialize both
    const cr_size = c.roaring_bitmap_portable_size_in_bytes(cr);
    const cr_buf = try allocator.alloc(u8, cr_size);
    defer allocator.free(cr_buf);

    const zr_size = zr.portable_size_in_bytes();
    const zr_buf = try allocator.alloc(u8, zr_size);
    defer allocator.free(zr_buf);
    var zr_w: std.Io.Writer = .fixed(zr_buf);
    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();
    _ = try zr.portable_serialize(&zr_w, arena.allocator());

    _ = c.roaring_bitmap_portable_serialize(cr, @ptrCast(cr_buf.ptr));
    try testing.expectEqual(cr_did_optimize, zr_did_optimize);
    try testing.expectEqual(cr_size, zr_size);
    try testing.expectEqualSlices(u8, cr_buf, zr_buf);

    // deserialize zr bytes with croaring
    const cr2 = c.roaring_bitmap_portable_deserialize_safe(@ptrCast(zr_buf.ptr), zr_size);
    try testing.expect(cr2 != null);
    defer c.roaring_bitmap_free(cr2);
    try testing.expectEqual(c.roaring_bitmap_get_cardinality(cr2), zr.cardinality());

    // deserialize croaring bytes with zr
    var cr_r: std.Io.Reader = .fixed(cr_buf);
    var zr2 = try Bitmap.portable_deserialize_safe(allocator, &cr_r);
    defer zr2.deinit(allocator);
    try testing.expect(zr.equals(zr2));
}

/// Validate FrozenBitmap can read serialized bytes and contains() works correctly.
fn validateFrozenContains(allocator: mem.Allocator, name: []const u8, values: []const u32, run_optimize: bool) !void {
    _ = name; // autofix

    // Build both and serialize frozen
    const cr = c.roaring_bitmap_create();
    defer c.roaring_bitmap_free(cr);
    c.roaring_bitmap_add_many(cr, values.len, values.ptr);
    if (run_optimize) _ = c.roaring_bitmap_run_optimize(cr);
    const cr_frozen_buf = try allocator.alloc(u8, c.roaring_bitmap_frozen_size_in_bytes(cr));
    defer allocator.free(cr_frozen_buf);
    c.roaring_bitmap_frozen_serialize(cr, cr_frozen_buf.ptr);
    // std.debug.print("{s} cr_frozen_size {}\n", .{ name, cr_frozen_size });

    var zr: Bitmap = .{};
    defer zr.deinit(allocator);
    try zr.add_many(allocator, values);
    if (run_optimize) _ = try zr.run_optimize(allocator);
    const zr_frozen_buf = try allocator.alloc(u8, zr.frozen_size_in_bytes());
    defer allocator.free(zr_frozen_buf);
    try zr.frozen_serialize(zr_frozen_buf);

    try testing.expectEqualSlices(u8, cr_frozen_buf, zr_frozen_buf);
}

fn validate() !void {
    const allocator = testing.allocator;
    // Basic tests:
    try validateRoundTrip(allocator, "empty", &.{}, false);
    try validateRoundTrip(allocator, "single_zero", &.{0}, false);
    try validateRoundTrip(allocator, "single_max", &.{0xFFFFFFFF}, false);
    try validateRoundTrip(allocator, "single_mid", &.{1000000}, false);

    // Array container tests:
    var arr100: [100]u32 = undefined; // Small array
    for (0..100) |i| arr100[i] = @intCast(i * 10);
    try validateRoundTrip(allocator, "array_100", &arr100, false);
    var arr4096: [4096]u32 = undefined; // Array at threshold (4096 = max array size)
    for (0..4096) |i| arr4096[i] = @intCast(i);
    try validateRoundTrip(allocator, "array_4096", &arr4096, false);

    // Bitset container tests:
    var bitset5000: [5000]u32 = undefined; // Just over threshold -> bitset
    for (0..5000) |i| bitset5000[i] = @intCast(i);
    try validateRoundTrip(allocator, "bitset_5000", &bitset5000, false);

    // Full chunk as run (65536 values) - CRoaring auto-optimizes to run, so we must too
    // (This tests run serialization, not bitset - renamed to avoid confusion)
    try validateRangeRoundTrip(allocator, "run_full_chunk", 0, 65535, true);

    // Multiple container tests:
    // Values at chunk boundaries
    try validateRoundTrip(allocator, "chunk_boundaries", &.{ 65535, 65536, 131071, 131072 }, false);
    // 3 containers (below NO_OFFSET_THRESHOLD for run format)
    var three_containers: [3]u32 = .{ 100, 65536 + 100, 131072 + 100 };
    try validateRoundTrip(allocator, "three_containers", &three_containers, false);
    // 4 containers (at NO_OFFSET_THRESHOLD)
    var four_containers: [4]u32 = .{ 100, 65536 + 100, 131072 + 100, 196608 + 100 };
    try validateRoundTrip(allocator, "four_containers", &four_containers, false);
    // 5+ containers
    var five_containers: [5]u32 = .{ 100, 65536 + 100, 131072 + 100, 196608 + 100, 262144 + 100 };
    try validateRoundTrip(allocator, "five_containers", &five_containers, false);

    // Run-optimized tests:
    // Range that compresses well
    try validateRangeRoundTrip(allocator, "range_0_1000", 0, 1000, true);
    try validateRangeRoundTrip(allocator, "range_0_10000", 0, 10000, true);
    // Multiple ranges -> multiple runs
    var multi_range: [300]u32 = undefined;
    for (0..100) |i| {
        multi_range[i] = @intCast(i); // 0-99
        multi_range[100 + i] = @intCast(500 + i); // 500-599
        multi_range[200 + i] = @intCast(1000 + i); // 1000-1099
    }
    try validateRoundTrip(allocator, "multi_range_runs", &multi_range, true);
    // Alternating values (doesn't compress to runs)
    var alternating: [100]u32 = undefined;
    for (0..100) |i| alternating[i] = @intCast(i * 2); // 0, 2, 4, 6...
    try validateRoundTrip(allocator, "alternating_no_runs", &alternating, true);
    // 4+ containers with run_optimize - exercises run format WITH offset header
    // (NO_OFFSET_THRESHOLD = 4, so this triggers offset header in run format)
    var four_chunks_runs: [400]u32 = undefined;
    for (0..100) |i| four_chunks_runs[i] = @intCast(i); // chunk 0: 0-99
    for (0..100) |i| four_chunks_runs[100 + i] = @intCast(65536 + i); // chunk 1
    for (0..100) |i| four_chunks_runs[200 + i] = @intCast(131072 + i); // chunk 2
    for (0..100) |i| four_chunks_runs[300 + i] = @intCast(196608 + i); // chunk 3
    try validateRoundTrip(allocator, "four_chunks_run_optimized", &four_chunks_runs, true);

    // Large scale tests:
    // Dense range (1M values) - CRoaring auto-optimizes ranges, so we must too
    try validateRangeRoundTrip(allocator, "dense_1M", 0, 999999, true);

    // Sparse random (N values across u32 space)
    const N = if (std.debug.runtime_safety) 2000 else 500000;
    var prng = std.Random.DefaultPrng.init(0);
    const sparse_N = try allocator.alloc(u32, N);
    defer allocator.free(sparse_N);
    for (sparse_N) |*x| x.* = prng.random().int(u32);
    // Sort and dedupe for consistent results
    std.mem.sort(u32, sparse_N, {}, std.sort.asc(u32));
    var deduped_len: usize = 1;
    for (1..N) |i| {
        if (sparse_N[i] != sparse_N[deduped_len - 1]) {
            sparse_N[deduped_len] = sparse_N[i];
            deduped_len += 1;
        }
    }
    try validateRoundTrip(allocator, "sparse_N", sparse_N[0..deduped_len], false);

    // validate frozen_view can read serialized bytes correctly
    try validateFrozenContains(allocator, "frozen_array", &arr100, false);
    try validateFrozenContains(allocator, "frozen_bitset", &bitset5000, false);
    try validateFrozenContains(allocator, "frozen_run_single_chunk", &multi_range, true);
    try validateFrozenContains(allocator, "frozen_run_with_offsets", &four_chunks_runs, true);
    try validateFrozenContains(allocator, "frozen_multi_container", &five_containers, false);
}

test validate {
    try validate();
}

fn validateTestdata(filepath: []const u8) !void {
    const f = try std.fs.cwd().openFile(filepath, .{});
    defer f.close();
    var rbuf: [256]u8 = undefined;
    var freader = f.reader(&rbuf);
    var rb = try Bitmap.portable_deserialize(testing.allocator, &freader.interface);
    defer rb.deinit(testing.allocator);

    // > That is, they contain all multiplies of 1000 in [0,100000), all multiplies of 3 in [100000,200000) and all values in [700000,800000).
    // > https://github.com/RoaringBitmap/RoaringFormatSpec/tree/master/testdata
    var k: u32 = 0;
    while (k < 100000) : (k += 1000)
        try testing.expect(rb.contains(k));

    k = 100000;
    while (k < 200000) : (k += 1)
        try testing.expect(rb.contains(3 * k));

    k = 700000;
    while (k < 800000) : (k += 1)
        try testing.expect(rb.contains(k));
}

test "without runs" {
    try validateTestdata("testdata/bitmapwithoutruns.bin");
}

test "with runs" {
    try validateTestdata("testdata/bitmapwithruns.bin");
}

const std = @import("std");
const mem = std.mem;
const testing = std.testing;
const zroaring = @import("root.zig");
const Bitmap = zroaring.Bitmap;
const c = @cImport({
    // @cDefine("CROARING_COMPILER_SUPPORTS_AVX512", "0");
    @cDefine("CROARING_ATOMIC_IMPL", "1");
    @cInclude("c/roaring.h");
});
